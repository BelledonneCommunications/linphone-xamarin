// Project information
buildDir = 'bin'

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.0'
    }
}

allprojects {
    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
    }
}

apply plugin: 'com.android.library'

dependencies {
    compile group: 'org.apache.commons', name: 'commons-compress', version: '1.5'
}

def submoduleDir = ['submodules/mediastreamer2/java/src',
                    'submodules/linphone/java/j2se',
                    'submodules/linphone/java/common',
                    'submodules/linphone/java/impl',
                    'submodules/linphone/coreapi/help/java']

android {
    defaultConfig {
        compileSdkVersion 25
        buildToolsVersion "25.0.2"
        multiDexEnabled true
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    sourceSets {
        main {
            manifest.srcFile 'Xamarin/Xamarin/Xamarin.Droid/Properties/AndroidManifest.xml'
            java.srcDirs = submoduleDir
            resources.srcDirs = submoduleDir
            aidl.srcDirs = submoduleDir
            renderscript.srcDirs = submoduleDir
            jniLibs.srcDirs = ['Xamarin/Xamarin/Xamarin.Droid/Libs']

            java.excludes = ['**/mediastream/MediastreamerActivity.java']

            // Exclude some useless files
            packagingOptions {
                exclude '**/gdb.*'
            }
        }
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }
}

///////////// Task /////////////

task classJar(type: Jar) {
    classifier = 'class'
    from 'intermediates/classes/release'
    include '**/*.class'
    libsDirName = '../Xamarin/Xamarin/Xamarin.Droid/Libs'
    archiveName = 'liblinphone.jar'
}
